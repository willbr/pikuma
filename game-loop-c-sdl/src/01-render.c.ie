#include stdlib("stdio.h")
#include stdlib("SDL2/SDL.h")

#define WINDOW-WIDTH 800
#define WINDOW-HEIGHT 600

struct ball
    x      float
    y      float
    width  float
    height float

vars
    window          *SDL_Window   null
    renderer        *SDL_Renderer null
    game-is-running int           false
    ball            [struct ball] init-block(0)


fn main
    args
        argc int
        argv **char
    returns int

    game-is-running = initialise-window

    setup

    while game-is-running
        process-input
        update
        render

    destroy-window

    return 0

fn update
    null-statement
    return


fn render
    SDL-SetRenderDrawColor renderer 0 0 0 255
    SDL-RenderClear renderer

    var ball-rect SDL-Rect
        init-block(x,y,w,h)
            \ ball.x
            \ ball.y
            \ ball.width
            \ ball.height

    SDL-SetRenderDrawColor renderer 255 255 255 255
    SDL-RenderFillRect renderer &ball-rect

    SDL-RenderPresent renderer
    return

fn destroy-window
    SDL-DestroyRenderer renderer
    SDL-DestroyWindow   window
    SDL-Quit


fn setup
    ball.x =20
    ball.y =20
    ball.width  = 15
    ball.height = 15
    return


fn process-input
    var event SDL-Event init-block(0)

    SDL-PollEvent &event

    switch event.type
        SDL-QUIT
            game-is-running = false
            break

        SDL-KEYDOWN
            if event.key.keysym.sym == SDLK-ESCAPE
                game-is-running = false
            break

    return



fn initialise-window
    returns int

    if SDL-Init(SDL-INIT-EVERYTHING) != 0
        fprintf stderr "Error: initialising SDL.\n"
        return false

    window =
        SDL-CreateWindow
            \ null
            \ SDL-WINDOWPOS-CENTERED
            \ SDL-WINDOWPOS-CENTERED
            \ WINDOW-WIDTH
            \ WINDOW-HEIGHT
            \ SDL-WINDOW-BORDERLESS

    if !window
        fprintf stderr "Error creating SDL Window.\n"
        return false

    renderer =
        SDL-CreateRenderer
            \ window -1 0

    if !renderer
        fprintf stderr "Error creating SDL Renderer.\n"
        return false


    return true

